
allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'maven'
    apply plugin: 'signing'

    group = 'org.jpos'
    version = '1.9.0'
    def isSnapshot = version.contains("SNAPSHOT")
    def mavenCentralRepo = isSnapshot ?
        'https://oss.sonatype.org/content/repositories/snapshots/' : 
        'https://oss.sonatype.org/service/local/staging/deploy/maven2';

    sourceCompatibility = 1.6
    targetCompatibility = 1.6
    repositories {
        mavenLocal()
        mavenCentral()
        mavenRepo name: 'oracle', url: "http://download.oracle.com/maven"
        mavenRepo name: 'jline', url: "http://jline.sourceforge.net/m2repo"
    }
    def pomConfig = {
        name 'jPOS Project'
        description """
    jPOS is an ISO-8583 based financial transaction 
    library/framework that can be customized and 
    extended in order to implement financial interchanges.
  """
        url 'http://jpos.org'
        organization {
            name 'jPOS.org'
            url 'http://jpos.org'
        }
        issueManagement {
            system 'YouTrack'
            url 'https://jpos.org/issues'
        }
        scm {
            url "http://github.com/jpos/jPOS"
            connection "scm:git:https://github.com/jpos/jPOS.git"
            developerConnection "scm:git:git@github.com:jpos/jPOS.git"
        }
        licenses {
            license {
                name 'GNU AFFERO GENERAL PUBLIC LICENSE'
                url 'http://www.gnu.org/licenses/agpl-v3.html'
                comments 'See http://jpos.org/license for more details.'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'jpos-team'
                name 'jPOS Development Team'
                email 'info@jpos.org'
            }
        }
    }
    signing {
        required { !isSnapshot }
        sign configurations.archives
    }
    configure(install.repositories.mavenInstaller) {
        pom.project pomConfig
    }
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                repository(url: 'file:///opt/local/maven')
                repository(url: mavenCentralRepo) {
                    authentication(userName: mavenCentralUsername, password: mavenCentralPassword)
                }
            }
        }
    }
}

subprojects  {
    configurations.compile.transitive = true
}

task aggregatedJavadoc (type: Javadoc, description: "Aggregated Javadocs") { 
    source subprojects.collect {project -> 
        project.sourceSets.main.allJava 
    } 
    destinationDir = new File(buildDir, 'docs/javadoc') 
    classpath = files(subprojects.collect {project -> 
        project.sourceSets.main.compileClasspath}) 
} 

task wrapper( type: Wrapper ) {
    gradleVersion = '1.4'
}

// Configure IDEA to use Git
idea.project.ipr {
  withXml { provider ->
    provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git'
  }
}

